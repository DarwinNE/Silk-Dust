AWS
VERSIONE
3.0
0
7
7
0
Silk Dust, parts 3, 4
Davide Bucci
Version 1.0.3, Sept. 2020 - May 2025

1

0
0
130
FALSE
0
0
CONDIZIONIHI
IF AT 102 THEN PRESSKEY GOTO 71 ENDIF Print the introduction of part 3 and wait a key.
IF AT 77 AND SET? 70 THEN MESS 655 GOTO 103 ENDIF End of part 3.
IF AT 103 THEN RESETALL RESE 124 PRESSKEY TO 197 1500 TO 249 1500 TO 246 1500 TO 198 1500 SET 60 GOTO 90 ENDIF Print the introduction of part 4 and wait a key.
IF AT 93 THEN MESS 682 EXIT ENDIF End of the game.
IF AT 90 AND SET? 80 AND RES? 81 THEN MESS 685 SET 81 ENDIF The police arrives in the fisherman's harbour. Police officers follow your moves.
IF SET? 81 THEN BRIN 254 MESS 689 ENDIF Police follows you.
CONDIZIONILOW
if verb 73 then quit endif
if verb 74 or verb 0 and noun 72 then inve wait endif
if verb 987 then restart endif
if verb 110 then look wait endif
IF verb 100 OR vbnoeq 0 AND noun 80 THEN nord WAIT ENDIF  Go north.
IF verb 100 OR vbnoeq 0 AND noun 81 AND NO2EQ 0 THEN sud WAIT ENDIF   Go south. Require noun2=0 to avoid "a" (aft) to be falsely recognized in situations such as "burn a match".
IF verb 100 OR vbnoeq 0 AND noun 82 THEN est WAIT ENDIF   Go east.
IF verb 100 OR vbnoeq 0 AND noun 83 THEN ovest WAIT ENDIF Go west.
IF verb 100 OR vbnoeq 0 AND noun 84 THEN alto WAIT ENDIF  Go up.
IF verb 100 OR vbnoeq 0 AND noun 85 THEN basso WAIT ENDIF Go down.
IF AT 61 OR AT 63 AND VERB 70 AND NOUN 51 THEN MESS 679 WAIT ENDIF Examine deckchairs
IF VERB 70 AND ACTOR 186 AND AVAI 186 THEN MESS 600 WAIT ENDIF Examine captain Maalouf.
IF VERB 75 AND ACTOR 186 AND AVAI 186 THEN MESS 611 WAIT ENDIF Talk to captain Maalouf.
IF VERB 70 AND ACTOR 25 AND AVAI 25 AND RES? 60 THEN MESS 601 WAIT ENDIF Examine Albert.
IF VERB 70 AND ACTOR 25 AND AVAI 25 THEN MESS 675 WAIT ENDIF Examine Albert.
IF VERB 75 AND ACTOR 25 AND AVAI 25 AND RES? 60 THEN MESS 622 WAIT ENDIF Talk to Albert.
IF VERB 75 AND ACTOR 25 AND AVAI 25 THEN MESS 625 WAIT ENDIF Talk to Albert.
IF VERB 406 AND ACTOR 25 AND AVAI 25 AND RES? 60 THEN MESS 602 SET 60 WAIT ENDIF Kiss Albert.
IF VERB 406 AND ACTOR 25 AND AVAI 25 THEN MESS 663 WAIT ENDIF Kiss Albert a second time.
IF VERB 70 AND ACTOR 24 AND AVAI 24 THEN MESS 603 WAIT ENDIF Examine Briccorosso.
IF VERB 70 AND ACTOR 187 AND AVAI 187 THEN MESS 604 WAIT ENDIF Examine Youssef.
IF VERB 75 AND ACTOR 187 AND AVAI 187 AND RES? 61 THEN MESS 605 SET 61 WAIT ENDIF Talk to Youssef.
IF VERB 75 AND ACTOR 187 AND AVAI 187 THEN MESS 66 WAIT ENDIF Talk to Youssef a second time.
IF VERB 70 AND ACTOR 188 AND AVAI 188 THEN MESS 608 WAIT ENDIF Examine Sarah.
IF ACTOR 26 OR ACTOR 175 AND VERB 70 AND AVAI 189 THEN MESS 607 WAIT ENDIF Examine Toyo.
IF VERB 75 AND ACTOR 26 AND AVAI 189 THEN MESS 66 WAIT ENDIF  Talk to Toyo.
IF VERB 75 AND ACTOR 188 AND AVAI 188 THEN MESS 609 WAIT ENDIF  Talk to Sarah.
IF VERB 70 AND NOUN 178 AND AVAI 192 THEN MESS 610 WAIT ENDIF Examine the steam engine.
IF VERB 70 AND NOUN 193 AND AVAI 193 AND RES? 71 THEN MESS 620 BRIN 194 SET 71 WAIT ENDIF Examine luggage, find the umbrella.
IF VERB 70 AND NOUN 193 AND AVAI 193 THEN MESS 678 WAIT ENDIF Examine luggage after having found the umbrella -> nothing.
IF VERB 70 AND NOUN 194 AND AVAI 194 THEN MESS 621 WAIT ENDIF Examine umbrella.
IF VERB 70 AND NOUN 195 AND AVAI 195 AND SET? 67 THEN MESS 624 MESS 711 WAIT ENDIF Examine cannon.
IF VERB 70 AND NOUN 195 AND AVAI 195 THEN MESS 624 MESS 710 WAIT ENDIF Examine cannon.
IF VERB 50 AND NOUN 239 AND AVAI 197 THEN GET 197 OKAY ENDIF Get hammer.
IF VERB 70 AND NOUN 239 AND AVAI 197 THEN MESS 658 WAIT ENDIF Examine hammer.
IF VERB 70 AND NOUN 198 AND AVAI 198 THEN MESS 694 WAIT ENDIF Examine nails.
IF VERB 50 AND NOUN 3 THEN GET 244 OKAY ENDIF Get paper.
IF VERB 50 AND NOUN 199 AND AVAI 244 THEN TO 199 0 TO 244 0 TO 245 1500 OKAY ENDIF Get gunpowder, have the wax paper.
IF VERB 50 AND NOUN 199 THEN MESS 635 WAIT ENDIF Get gunpowder.
IF VERB 70 AND NOUN 173 AND AVAI 252 THEN MESS 657 WAIT ENDIF Examine the pieces of the blown padlock.
IF VERB 70 AND NOUN 247 AND AVAI 247 THEN MESS 658 WAIT ENDIF Examine the padlock.
IF ROOMGT 66 AND ROOMLT 84 AND VERB 70 AND NOUN 58 THEN MESS 660 WAIT ENDIF Examine lamp/lamps.
IF VERB 70 AND NOUN 248 OR NOUN 249 AND AVAI 248 OR AVAI 249 THEN MESS 694 WAIT ENDIF Examine gun/knife.
IF VERB 396 AND AVAI 248 THEN MESS 505 WAIT ENDIF Shot.
IF VERB 157 AND NO1EQ 241 AND NO2EQ 249 AND AVAI 241 AND AVAI 249 THEN MESS 718 TO 241 0 TO 242 1500 WAIT ENDIF Sharpen wedge, mention the knife
IF VERB 157 AND NOUN 241 AND AVAI 241 THEN MESS 714 WAIT ENDIF Sharpen wedge, don't mention the knife
IF ROOMGT 66 AND ROOMLT 84 AND VERB 50 AND NOUN 58 THEN MESS 665 WAIT ENDIF Get lamp -> no!
IF AT 67 OR AT 71 OR AT 70 OR AT 73 OR AT 77 AND VERB 15 AND NOUN 18 THEN MESS 400 WAIT ENDIF Use bed.
IF AT 67 OR AT 71 OR AT 70 OR AT 73 OR AT 77 AND VERB 70 AND NOUN 18 THEN MESS 671 WAIT ENDIF Examine bed.
IF AT 70 OR AT 71 OR AT 72 OR AT 73 OR AT 77 AND VERB 70 AND NOUN 51 THEN MESS 664 WAIT ENDIF Examine table.
IF VERB 143 AND ROOMLT 70 THEN MESS 674 WAIT ENDIF Smell...
IF VERB 75 AND ACTOR 24 AND AVAI 24 THEN MESS 667 WAIT ENDIF Talk to Briccorosso.
IF VERB 75 AND ACTOR 190 AND AVAI 190 THEN MESS 609 WAIT ENDIF Talk to Neda.
IF VERB 75 AND ACTOR 191 AND AVAI 191 THEN MESS 505 WAIT ENDIF Talk to Roy.
IF VERB 75 AND ACTOR 254 AND AVAI 254 THEN MESS 693 WAIT ENDIF Talk to police officers.
IF VERB 70 AND ACTOR 191 AND AVAI 191 THEN MESS 670 WAIT ENDIF Examine Roy.
IF VERB 70 AND ACTOR 254 AND AVAI 254 THEN MESS 690 WAIT ENDIF Examine police officers.
IF VERB 70 AND ACTOR 190 AND AVAI 190 THEN MESS 669 WAIT ENDIF Examine Neda.
IF VERB 70 AND NOUN 241 AND AVAI 241 THEN MESS 717 WAIT ENDIF Examine blunt wedge.
IF VERB 70 AND NOUN 241 AND AVAI 242 THEN MESS 718 WAIT ENDIF Examine sharp wedge.
IF VERB 50 AND NOUN 399 THEN MESS 401 WAIT ENDIF Get naked -> no!
IF VERB 201 THEN MESS 400 WAIT ENDIF Sleep
IF VERB 122 THEN MESS 403 WAIT ENDIF Wait... Oh, no!
IF VERB 12 and noun 10 THEN MESS 401 WAIT ENDIF Open window -> no
IF VERB 230 AND NOUN 233 THEN GOTO 103 ENDIF Password, goto part 4.
IF VERB 1000 then MESS 998 WAIT ENDIF XYZZY
IF VERB 400 THEN mess 504 WAIT ENDIF Curse
IF VERB 401 THEN MESS 61 WAIT ENDIF Pee
IF VERB 143 THEN MESS 997 WAIT ENDIF Smell...
IF VERB 66 THEN MESS 401 WAIT ENDIF Give...
IF VERB 211 OR VERB 406 OR VERB 67 THEN mess 401 WAIT ENDIF Touch..., Drink..., Wash...
IF VERB 1 AND NO1EQ 124 AND ACTOR 25 AND AVAI 25 THEN MESS 662 WAIT ENDIF Make love with Albert...
IF VERB 1 AND NOUN 124 THEN MESS 661 WAIT ENDIF Make love...
IF VERB 50 AND NO1GT 24 AND NO1LT 50 THEN GET NO1 OKAY ENDIF Take an object
IF VERB 55 AND NO1GT 24 AND NO1LT 50 THEN DROP NO1 OKAY ENDIF Drop an object
IF VERB 50 AND NO1GT 169 AND NO1LT 200 THEN GET NO1 OKAY ENDIF Take an object
IF VERB 55 AND NO1GT 169 AND NO1LT 200 THEN DROP NO1 OKAY ENDIF Drop an object
IF VERB 50 AND NO1GT 239 AND NO1LT 256 THEN GET NO1 OKAY ENDIF Take an object
IF VERB 55 AND NO1GT 239 AND NO1LT 256 THEN DROP NO1 OKAY ENDIF Drop an object
IF VERB 50 THEN MESS 401 WAIT ENDIF Take...
IF VERB 407 THEN MESS 407 WAIT ENDIF
IF VERB 70 AND NOUN 79 THEN LOOK WAIT ENDIF Examine room = look
IF VERB 210 THEN MESS 231 WAIT ENDIF Pray
IF VERB 70 THEN MESS 996 WAIT ENDIF Examine (without a known object)
IF VERB 15 THEN MESS 408 WAIT ENDIF Use... "use a more specific verb".
IF VERB 78 THEN MESS 122 WAIT ENDIF Break/Destroy/Kill...
IF VERB 988 THEN LOAD WAIT ENDIF Load
IF VERB 989 THEN SAVE WAIT ENDIF Save
IF VERB 76 THEN MESS 402 WAIT ENDIF Exit/Quit -> use "bye"
CONDIZIONILOCALI
61
IF SET? 60 AND SET? 61 THEN MESS 612 SET 62 RESE 61 GOTO 71 ENDIF Eat with the crew, then go to the cabin.
61
IF VERB 100 OR VBNOEQ 0 AND NOUN 82 AND SET? 63 THEN MESS 626 WAIT ENDIF   Go starboard -> No, pirates are present!
62
IF VERB 70 AND NOUN 68 THEN MESS 672 WAIT ENDIF Examine parquet.
65
IF VERB 100 OR VBNOEQ 0 AND NOUN 80 AND SET? 63 THEN MESS 626 WAIT ENDIF   Go fore -> No, pirates are present!
65
IF VERB 100 OR VBNOEQ 0 AND NOUN 82 AND SET? 63 THEN MESS 626 WAIT ENDIF   Go starboard -> No, pirates are present!
65
IF VERB 70 AND NOUN 204 AND SET? 63 THEN MESS 627 BRIN 196 WAIT ENDIF Find the manhole near the hatch.
65
IF VERB 70 AND NOUN 204 THEN MESS 629 WAIT ENDIF Examine the hatch, not very attentively.
65
IF VERB 12 AND NOUN 196 THEN MESS 628 SETCONN 65 6 75 SETCONN 75 5 65 SETCONN 75 6 82 SETCONN 82 5 75 WAIT ENDIF Open the manhole.
65
IF VERB 70 AND NOUN 196 THEN MESS 630 WAIT ENDIF Examine the manhole.
65
IF VERB 203 AND NOUN 196 THEN MESS 401 WAIT ENDIF Close the manhole.
65
IF VERB 100 OR VBNOEQ 0 AND NOUN 81 AND NO2EQ 0 AND RES? 63 THEN MESS 673 WAIT ENDIF   Go south -> No!
65
IF VERB 70 AND NOUN 398 THEN MESS 676 WAIT ENDIF Examine hatch.
65
IF VERB 70 AND NOUN 397 OR NOUN 90 THEN MESS 677 WAIT ENDIF Examine crane.
67
IF VERB 70 AND NOUN 51 AND RES? 72 THEN MESS 654 BRIN 248 SET 72 WAIT ENDIF Examine desk ->  find the gun.
67
IF VERB 70 AND NOUN 51 THEN MESS 678 WAIT ENDIF Examine desk -> nothing.
67
IF VERB 70 AND NOUN 200 THEN MESS 680 WAIT ENDIF Examine library.
70
IF VERB 70 AND NOUN 200 OR NOUN 207 THEN MESS 668 WAIT ENDIF Examine books/maps.
70
IF VERB 70 AND NOUN 144 THEN MESS 695 WAIT ENDIF Examine furniture.
71
IF VERB 100 OR VBNOEQ 0 AND NOUN 82 AND SET? 63 THEN MESS 615 WAIT ENDIF Exit from your cabin with pirates onboard.
71
IF VERB 100 OR VBNOEQ 0 AND NOUN 82 AND SET? 62 THEN MESS 613 SET 63 SETCONN 75 2 0 TO 191 77 TO 190 77 TO 189 77 TO 188 77 TO 187 77 WAIT ENDIF Exit from your cabin and hear the pirates (the door leading to crew quarters will be shut by pirates).
71
IF VERB 70 AND NOUN 10 AND SET? 63 THEN MESS 614 SET 65 WAIT ENDIF See the pirates' ship in front of your cabin.
71
IF VERB 70 AND NOUN 10 THEN MESS 666 WAIT ENDIF See the sea from the porthole.
71
IF VERB 12 AND NOUN 10 AND SET? 65 AND RES? 64 THEN SET 64 MESS 616 WAIT ENDIF Open porthole.
71
IF VERB 203 AND NOUN 10 AND SET? 65 AND SET? 64 THEN RESE 64 OKAY ENDIF Close porthole.
71
IF VERB 70 AND NOUN 13 AND SET? 66 THEN MESS 619 BRIN 193 WAIT ENDIF Examine open closet.
71
IF VERB 70 AND NOUN 13 THEN MESS 617 WAIT ENDIF Examine closet in your cabin.
71
IF VERB 12 AND NOUN 13 AND SET? 63 THEN MESS 618 SET 66 WAIT ENDIF Open closet (after being attacked by pirates).
71
IF VERB 12 AND NOUN 13 THEN MESS 505 WAIT ENDIF Open closet -> you don't find the need.
71
IF VERB 69 AND NOUN 220 AND AVAI 194 THEN                                          MESS 623 SETCONN 71 6 84 SETCONN 71 5 61 SETCONN 61 6 71 SET 77  WAIT ENDIF Pull the ladder.
71
IF VERB 15 AND NO1EQ 194 AND NO2EQ 220 AND AVAI 194 THEN                           MESS 623 SETCONN 71 6 84 SETCONN 71 5 61 SETCONN 61 6 71 SET 77 WAIT ENDIF Use the umbrella with the rope ladder.
71
IF VERB 50 AND NO1EQ 194 OR NO1EQ 220 AND NO1EQ 220 OR NO2EQ 220 AND AVAI 194 THEN MESS 623 SETCONN 71 6 84 SETCONN 71 5 61 SETCONN 61 6 71 SET 77 WAIT ENDIF Use the umbrella with the rope ladder.
71
IF VERB 100 OR VBNOEQ 0 AND NOUN 84 AND SET? 77 THEN ALTO WAIT ENDIF  Go up.
74
IF VERB 70 AND NOUN 27 AND SET? 63 AND RES? 73 THEN MESS 637 BRIN 244 SET 73 WAIT ENDIF Find the wax paper.
74
IF VERB 70 AND NOUN 27 OR NOUN 173 OR NOUN 395 THEN MESS 697 WAIT ENDIF Examine oil/cans/paraffin/jars/canisters etc...
75
IF SET? 69 AND VERB 100 OR VBNOEQ 0 AND NOUN 80 OR NOUN 82 OR NOUN 83 OR NOUN 84 OR NOUN 85 THEN MESS 659 WAIT ENDIF Go in a wrong direction after having blown the padlock.
75
IF VERB 100 OR VBNOEQ 0 AND NOUN 80 AND SET? 63 THEN MESS 626 WAIT ENDIF   Go fore -> No, pirates are present!
75
IF VERB 70 AND NOUN 4 AND SET? 63 AND RES? 69 THEN MESS 638 BRIN 247 WAIT ENDIF Examine door: see the large padlock.
75
IF VERB 71 AND NOUN 199 AND NO2EQ 247 AND AVAI 247 AND AVAI 245 THEN SET 70 TO 247 0 TO 245 0 MESS 650 WAIT ENDIF Put gunpowder in the padlock.
75
IF VERB 235 AND NOUN 199 AND SET? 70 AND RES? 69 AND RES? 67 THEN MESS 656 WAIT ENDIF Ignite the gunpowder without having spiked the cannon.
75
IF VERB 235 AND NOUN 199 AND SET? 70 AND RES? 69 AND AVAI 248 AND AVAI 249 THEN MESS 651 SET 69 SETCONN 75 2 77 TO 247 0 BRIN 252 WAIT ENDIF Explode the padlock and open the door of the crew's quarters.
75
IF VERB 235 AND NOUN 199 AND SET? 70 AND RES? 69 THEN MESS 653 WAIT ENDIF Explode the padlock without enough arms.
75
IF NO1EQ 246 AND NO2EQ 247 AND SET? 70 AND RES? 69 AND RES? 67 THEN MESS 656 WAIT ENDIF Ignite the gunpowder without having spiked the cannon.
75
IF NO1EQ 246 AND NO2EQ 247 AND SET? 70 AND RES? 69 AND AVAI 248 AND AVAI 249 THEN MESS 651 SET 69 SETCONN 75 2 77 TO 247 0 BRIN 252 WAIT ENDIF Explode the padlock and open the door of the crew's quarters.
75
IF NO1EQ 246 AND NO2EQ 247 AND SET? 70 AND RES? 69 THEN MESS 653 WAIT ENDIF Explode the padlock without enough arms.
75
IF VERB 70 AND NOUN 4 THEN MESS 658 WAIT ENDIF Examine door.
76
IF VERB 70 AND NOUN 206 AND SET? 63 AND RES? 74 THEN MESS 632 BRIN 197 BRIN 198 SET 74 WAIT ENDIF Examine tools.
76
IF VERB 70 AND NOUN 29 THEN MESS 696 WAIT ENDIF Examine car.
76
IF VERB 70 AND NOUN 27 THEN MESS 698 WAIT ENDIF Examine paraffin/cans. 
76
IF VERB 70 AND NOUN 206 OR NOUN 173 OR NOUN 395 THEN MESS 697 WAIT ENDIF Examine tools/pieces/tyres/oil.
77
IF VERB 70 AND NOUN 10 THEN MESS 666 WAIT ENDIF See the sea from the porthole.
77
IF VERB 70 AND NOUN 18 THEN MESS 699 WAIT ENDIF Examine berths.
77
IF VERB 70 AND NOUN 394 THEN MESS 700 WAIT ENDIF Examine curtains.
77
IF VERB 70 AND NOUN 13 THEN MESS 701 WAIT ENDIF Examine cabinets.
80
IF VERB 70 AND NOUN 246 AND RES? 75 THEN MESS 640 BRIN 246 SET 75 WAIT ENDIF Search compartments.
81
IF VERB 70 AND NOUN 51 AND SET? 63 AND RES? 76 THEN MESS 652 BRIN 249 SET 76 WAIT ENDIF Search the workbench in the kitchen.
81
IF VERB 70 AND NOUN 51 AND SET? 63 THEN MESS 678 WAIT ENDIF Search a second time the workbench.
81
IF VERB 100 OR VERB 0 AND NOUN 80 AND AVAI 187 THEN MESS 606 WAIT ENDIF  Go fore. -> Can't, while the cook is in the kitchen.
81
IF VERB 100 OR VBNOEQ 0 AND NOUN 84 AND SET? 63 THEN MESS 626 WAIT ENDIF   Go up -> No, pirates are present!
81
IF VERB 70 AND NOUN 51 OR NOUN 59 THEN MESS 702 WAIT ENDIF Examine stove/workbench/basin.
81
IF VERB 143 THEN MESS 705 WAIT ENDIF Smell.
82
IF VERB 70 AND NOUN 206 THEN MESS 703 WAIT ENDIF Examine fire extinguishing tools.
82
IF VERB 70 AND NOUN 393 OR NOUN 28 THEN MESS 704 WAIT ENDIF Examine reservoir/steam generators.
82
IF VERB 143 THEN MESS 706 WAIT ENDIF Smell.
83
IF VERB 143 THEN MESS 706 WAIT ENDIF Smell.
83
IF VERB 70 AND NOUN 392 THEN MESS 707 WAIT ENDIF Examine bulkhead.
83
IF VERB 70 AND NOUN 393 THEN MESS 708 WAIT ENDIF Examine coal.
83
IF VERB 70 AND NOUN 390 THEN MESS 709 WAIT ENDIF Examine container.
84
IF VERB 206 AND NOUN 195 AND AVAI 197 AND AVAI 198 AND RES? 68 THEN MESS 634 WAIT ENDIF Spike the cannon, loaded.
84
IF NO1EQ 198 AND NO2EQ 195 AND AVAI 197 AND AVAI 198 AND RES? 68 THEN MESS 634 WAIT ENDIF Spike the cannon, loaded.
84
IF NO1EQ 239 AND NO2EQ 195 AND AVAI 197 AND AVAI 198 AND RES? 68 THEN MESS 634 WAIT ENDIF Spike the cannon, loaded.
84
IF VERB 69 AND NOUN 195 AND RES? 68 THEN MESS 636 SET 68 BRIN 199 WAIT ENDIF Push or pull the cannon.
84
IF VERB 69 AND NOUN 195 THEN MESS 401 WAIT ENDIF Push or pull the cannon a second time.
84
IF VERB 205 AND NOUN 195 THEN MESS 635 WAIT ENDIF Unload the cannon -> how?
84
IF VERB 206 AND NOUN 195 AND AVAI 197 AND AVAI 198 AND SET? 68 AND RES? 67 THEN MESS 633 SET 67 WAIT ENDIF Spike the cannon, unloaded
84
IF NO1EQ 198 AND NO2EQ 195  AND AVAI 197 AND AVAI 198 AND SET? 68 AND RES? 67 THEN MESS 633 SET 67 WAIT ENDIF Spike the cannon, unloaded
84
IF NO1EQ 239 AND NO2EQ 195  AND AVAI 197 AND AVAI 198 AND SET? 68 AND RES? 67 THEN MESS 633 SET 67 WAIT ENDIF Spike the cannon, unloaded
90
IF RES? 80 AND verb 100 OR vbnoeq 0 AND noun 83 THEN MESS 712 WAIT ENDIF Go west.
90
IF VERB 70 AND NOUN 112 AND RES? 89 THEN MESS 719 BRIN 241 SET 89 WAIT ENDIF Find the blunt wedge.
91
IF VERB 70 OR VERB 12 AND NOUN 204 AND RES? 87 THEN MESS 713 WAIT ENDIF Try to open the hatch: it's locked.
91
IF VERB 71 AND NO1EQ 241 AND NO2EQ 204 AND AVAI 241 AND RES? 88 THEN MESS 715 WAIT ENDIF Try to force/put the blunt wedge in the hatch -> no success!
91
IF VERB 71 AND NO1EQ 241 AND NO2EQ 204 AND AVAI 242 AND RES? 88 THEN MESS 716 SET 88 WAIT ENDIF Try to force/put the sharp wedge in the hatch -> success!
91
IF NO1EQ 239 AND NO2EQ 241 AND SET? 88 AND RES? 87 THEN MESS 681 SET 87 BRIN 24 BRIN 25 WAIT ENDIF Hammer the wedge -> break the locker!
91
IF VERB 75 AND ACTOR 24 AND AVAI 24 THEN MESS 683 SET 80 WAIT ENDIF Talk to Briccorosso
91
IF VERB 75 AND ACTOR 25 AND AVAI 24 THEN MESS 684 WAIT ENDIF Talk to Albert
92
IF VERB 143 THEN MESS 691 WAIT ENDIF Smell...
92
IF VERB 75 AND ACTOR 255 AND RES? 80 THEN MESS 686 WAIT ENDIF Talk to innkeeper.
92
IF VERB 75 AND ACTOR 255 THEN MESS 688 WAIT ENDIF Talk to innkeeper.
92
IF VERB 70 AND ACTOR 255 THEN MESS 692 WAIT ENDIF Examine innkeeper.
92
IF VERB 70 AND NOUN 59 OR NOUN 6 AND RES? 91 THEN MESS 720 BRIN 240 SET 91 WAIT ENDIF Examine sink/bar.
92
IF VERB 70 AND NOUN 240 AND AVAI 240 AND RES? 90 THEN MESS 721 SET 90 WAIT ENDIF Discover stolen cutlery from Dynamene.
92
IF VERB 100 OR VERB 0 AND NOUN 83 AND RES? 90 THEN MESS 687 WAIT ENDIF Go west. -> innkeeper stops you.
130
IF VERB 230 AND NOUN 232 THEN RESETALL GOTO 102 ENDIF Enter password for the third part of the game.
DIZIONARIO
MAKE
1
VERBO
BOAT
2
NOME
BOATS
2
NOME
DRAPERIES
3
NOME
TAPESTRIES
3
NOME
TAPESTRY
3
NOME
PATTERN
3
NOME
PATTERNS
3
NOME
UPHOLSTERY
3
NOME
WALLPAPER
3
NOME
PAPER
3
NOME
DOOR
4
NOME
GATE
4
NOME
PORTAL
4
NOME
ARCH
4
NOME
PILLARS
4
NOME
PILLAR
4
NOME
BAR
6
NOME
WINDOW
10
NOME
PORTHOLE
10
NOME
PORTHOLES
10
NOME
DRAWER
11
NOME
DRAWERS
11
NOME
OPEN
12
VERBO
UNLOCK
12
VERBO
CLOSET
13
NOME
CLOSETS
13
NOME
CABINET
13
NOME
CABINETS
13
NOME
TUB
14
NOME
BATTHUB
14
NOME
USE
15
VERBO
LAY
15
VERBO
BICYCLE
17
NOME
BICYCLES
17
NOME
BIKE
17
NOME
MOTORBIKE
17
NOME
BIKES
17
NOME
MOTORBIKES
17
NOME
BED
18
NOME
BERTH
18
NOME
BERTHS
18
NOME
HOUSEPLANT
19
NOME
HOUSEPLANTS
19
NOME
FICUS
19
NOME
WAVES
20
NOME
FRESCOES
22
NOME
FRESCO
22
NOME
CEILING
22
NOME
PRINCESS
24
ATTORE
LUCILLA
24
ATTORE
BRICCOROSSO
24
ATTORE
MADAM
24
ATTORE
ALBERT
25
ATTORE
PINEL
25
ATTORE
MECHANIC
26
ATTORE
VITTORIO
26
ATTORE
TOYO
26
ATTORE
STOKER
26
ATTORE
TRIMMER
26
ATTORE
FIREMAN
26
ATTORE
CAN
27
NOME
TIN
27
NOME
JAR
27
NOME
JARS
27
NOME
KEROSENE
27
NOME
PARAFFIN
27
NOME
CANISTERS
27
NOME
CANISTER
27
NOME
CANS
27
NOME
GENERATOR
28
NOME
GENERATORS
28
NOME
CAR
29
NOME
CARS
29
NOME
ITA
29
NOME
150
29
NOME
100
29
NOME
SERGIO
30
ATTORE
GARDENER
30
ATTORE
BUCKET
32
NOME
POSTER
33
NOME
WHEEL
34
NOME
DOCK
35
NOME
ELISA
37
ATTORE
MAID
37
ATTORE
ACORNS
39
NOME
ACORN
39
NOME
LEAF
40
NOME
LEAVES
40
NOME
RAKE
41
NOME
GET
50
VERBO
GRAB
50
VERBO
TAKE
50
VERBO
HOOK
50
VERBO
TABLE
51
NOME
BENCH
51
NOME
WORKBENCH
51
NOME
CHAIR
51
NOME
CHAIRS
51
NOME
DECKCHAIR
51
NOME
DECKCHAIRS
51
NOME
DESK
51
NOME
THANK
52
VERBO
LAKE
53
NOME
VIEW
53
NOME
SHORE
53
NOME
VALLEY
53
NOME
PORCH
54
NOME
DROP
55
VERBO
LEAVE
55
VERBO
SET
55
VERBO
THROW
55
VERBO
TOSS
55
VERBO
DRIVE
56
VERBO
ENTER
56
VERBO
VILLA
57
NOME
CHANDELIER
58
NOME
LAMP
58
NOME
LAMPS
58
NOME
BURNER
58
NOME
BURNERS
58
NOME
BASIN
59
NOME
BASINS
59
NOME
SINK
59
NOME
STOVE
59
NOME
HERBS
60
NOME
BUDS
62
NOME
GIVE
66
VERBO
OFFER
66
VERBO
BRUSH
67
VERBO
WASH
67
VERBO
PARQUET
68
NOME
WALL
68
NOME
WALLS
68
NOME
PANEL
68
NOME
PANELS
68
NOME
PANELING
68
NOME
PLATE
68
NOME
PLATES
69
NOME
ACTIVATE
69
VERBO
PULL
69
VERBO
ACTION
69
VERBO
MOVE
69
VERBO
SHAKE
69
VERBO
PUSH
69
VERBO
PRESS
69
VERBO
DEPRESS
69
VERBO
EX
70
VERBO
EXAMINE
70
VERBO
X
70
VERBO
READ
70
VERBO
SEARCH
70
VERBO
SLIDE
71
VERBO
PUT
71
VERBO
INSERT
71
VERBO
I
72
NOME
INV
72
NOME
INVE
72
NOME
INVENTORY
72
NOME
BYE
73
VERBO
SHOW
74
VERBO
CHAT
75
VERBO
SPEAK
75
VERBO
TALK
75
VERBO
SHOUT
75
VERBO
EXIT
76
VERBO
STOP
76
VERBO
QUIT
76
VERBO
PLAY
77
VERBO
BREAK
78
VERBO
DESTROY
78
VERBO
STRANGLE
78
VERBO
KILL
78
VERBO
ROOM
79
NOME
N
80
NOME
FORE
80
NOME
F
80
NOME
NORTH
80
NOME
SOUTH
81
NOME
AFT
81
NOME
A
81
NOME
E
82
NOME
EAST
82
NOME
STARBOARD
82
NOME
S
82
NOME
W
83
NOME
WEST
83
NOME
PORT
83
NOME
P
83
NOME
ABOVE
84
NOME
U
84
NOME
UP
84
NOME
UPSTAIRS
84
NOME
BELOW
85
NOME
D
85
NOME
DOWN
85
NOME
DOWNSTAIRS
85
VERBO
UNDER
85
NOME
FILL
86
VERBO
WATER
87
NOME
BUY
88
VERBO
POLE
90
NOME
POST
90
NOME
PICK
95
VERBO
HOUSE
98
NOME
GO
100
VERBO
RUN
100
VERBO
WALK
100
VERBO
STONE
102
NOME
STONES
102
NOME
ROCK
102
NOME
ROCKS
102
NOME
ROSE
103
NOME
ROSES
103
NOME
SHIP
104
NOME
DYNAMENE
105
NOME
STORE
108
NOME
STORES
108
NOME
L
110
VERBO
LOOK
110
VERBO
REPAIR
111
VERBO
RUBBISH
112
NOME
GARBAGE
112
NOME
LITTER
112
NOME
PILE
112
NOME
WEAR
120
VERBO
UNDRESS
121
VERBO
UNWEAR
121
VERBO
WAIT
122
VERBO
STYLE
123
NOME
STYLES
123
NOME
LOVE
124
NOME
SMELL
143
VERBO
FURNITURE
144
NOME
CREDENZA
144
NOME
DRESSER
144
NOME
OBJECT
156
NOME
SHARPEN
157
VERBO
POLISH
157
VERBO
HONE
157
VERBO
GRIND
157
VERBO
STROP
157
VERBO
FLOOR
159
NOME
GROUND
159
NOME
LIFT
160
VERBO
FIRE
164
NOME
NICHE
165
NOME
LIGHT
166
NOME
WORK
168
NOME
APSES
169
NOME
CARD
171
NOME
50A
174
NOME
BUTTON
176
NOME
KEY
177
NOME
ENGINE
178
NOME
COMPRESSOR
179
NOME
VALVE
180
NOME
TELEGRAM
181
NOME
ANTONIO
182
ATTORE
MILIERI
182
ATTORE
HANDYMAN
183
ATTORE
SALVATORE
183
ATTORE
FRIAR
184
ATTORE
FRIARS
184
ATTORE
MAALOUF
186
ATTORE
CAPTAIN
186
ATTORE
KHALIL
186
ATTORE
YOUSSEF
187
ATTORE
COOK
187
ATTORE
ZINE
187
ATTORE
SARAH
188
ATTORE
MACHINIST
188
ATTORE
NEDA
190
ATTORE
DEPUTY
190
ATTORE
SECOND
190
ATTORE
ROY
191
ATTORE
BOATSWAIN
191
ATTORE
LUGGAGE
193
NOME
UMBRELLA
194
NOME
CANNON
195
NOME
MANHOLE
196
NOME
NAIL
198
NOME
NAILS
198
NOME
GUNPOWDER
199
NOME
LIBRARY
200
NOME
BOOK
200
NOME
BOOKS
200
NOME
SIT
201
VERBO
SLEEP
201
VERBO
CLOTHES
202
NOME
CLOSE
203
VERBO
HATCH
204
NOME
UNLOAD
205
VERBO
SPIKE
206
VERBO
TOOL
206
NOME
TOOLS
206
NOME
MAPS
207
NOME
MAP
207
NOME
PRAY
210
VERBO
DRINK
211
VERBO
EAT
211
VERBO
SWALLOW
211
VERBO
WORKER
212
NOME
WORKERS
212
NOME
TURN
213
VERBO
MIRROR
219
NOME
LADDER
220
NOME
SHIBBOLETH
230
VERBO
FEMENENRIE
232
NOME
FINIANTLABAGNA
233
NOME
IGNITE
235
VERBO
HAMMER
239
NOME
CUTLERY
240
NOME
FORK
240
NOME
SPOON
240
NOME
WEDGE
241
NOME
MATCH
246
NOME
MATCHES
246
NOME
BOX
246
NOME
BOXES
246
NOME
COMPARTMENTS
246
NOME
LOCK
247
NOME
PADLOCK
247
NOME
GUN
248
NOME
GUNS
248
NOME
KNIFE
249
NOME
OFFICERS
254
ATTORE
OFFICER
254
ATTORE
POLICE
254
ATTORE
INNKEEPER
255
ATTORE
OUT
300
VERBO
CONTAINERS
390
NOME
CONTAINER
390
NOME
COAL
391
NOME
BULKHEAD
392
NOME
RESERVOIR
393
NOME
CURTAIN
394
NOME
CURTAINS
394
NOME
OIL
395
NOME
LUBRICANT
395
NOME
LUBRICANTS
395
NOME
TIRES
395
NOME
TYRES
395
NOME
SHOT
396
VERBO
CRANE
397
NOME
COVER
398
NOME
NAKED
399
NOME
CURSE
400
VERBO
FUCK
400
VERBO
CUNT
400
VERBO
CUNTS
400
VERBO
PISS
400
VERBO
PEE
401
VERBO
SHIT
401
VERBO
CARESS
406
VERBO
TOUCH
406
VERBO
KISS
406
VERBO
SEDUCE
406
VERBO
HELP
407
VERBO
INFO
407
VERBO
HINT
408
VERBO
RESTART
987
VERBO
LOAD
988
VERBO
SAVE
989
VERBO
XYZZY
1000
VERBO
THEN
9999
SEPARATORE
AND
9999
SEPARATORE
LOCAZIONI
60
This is where the captain and the crew navigate and control the ferry.

Bridge
0
62
0
0
0
0
0
0
0
0
61
You are standing on a covered deck; there are wooden deckchairs pointing towards the sea.

Promenade deck (port)
0
64
62
0
0
0
0
0
0
0
62
You are inside the ferry's central deckhouse. Wood panels kept together by brass elements cover the floor, forming a parquet pattern.

Deckhouse
60
65
63
61
0
72
0
0
0
0
63
You are standing on a covered deck; there are wooden deckchairs pointing towards the sea.

Promenade deck (starboard)
0
66
0
62
0
0
0
0
0
0
64
Nothing but sea is visible to the horizon.

Deck (port)
61
0
65
0
0
0
0
0
0
0
65
You see the large hatch, through which the ferry is loaded, closed by a heavy metallic cover. A large crane on a king post is used for this, not far from the captain's cabin at the aft end of the ship.

Loading deck
62
67
66
64
0
0
0
0
0
0
66
You are standing next to the sea.

Deck (starboard)
63
0
0
65
0
0
0
0
0
0
67
It's a sparsely decorated place, lit by a small lamp. You see a desk with two chairs, a library, the captain's bed and a closet.

Captain's cabin
65
0
0
0
0
0
0
0
0
0
70
This is the cabin where Princess Briccorosso sleeps. It's larger than yours and the mahogany furniture is more refined, but apart from that it is not very different. You see books and maps open on her desk which is lit by an elegant table lamp.

Briccorosso's cabin
0
72
0
0
0
0
0
0
0
0
71
This is your home during the journey. It is clean but is not very large, equipped with a bed, a small table with two chairs and a closet. This gives a monastic appearance to this place, lit by a small paraffin lamp. You see the calm Mediterranean sea from a porthole.

Emilia's cabin
0
0
72
0
0
0
0
0
0
0
72
This is a relatively large room lit by a paraffin lamp, with a long table where you and all the other passengers eat and spend time together in the evenings before going to sleep.

Dining room
70
75
73
71
62
81
0
0
0
0
73
This is the place where Albert sleeps, very similar to your cabin.

Albert's cabin
0
0
0
72
0
0
0
0
0
0
74
Several items that Briccorosso prepared for the adventure are here: numerous large canisters containing spare pieces, large jars of paraffin, cans of oil and other lubricants.

Hold (port)
0
0
75
0
0
0
0
0
0
0
75
You stand in a large hold where goods are stored for transport. An imposing iron door leads aft towards the crew's quarters.

Main hold
72
77
76
74
0
0
0
0
0
0
76
Many goods are stored here: cans of paraffin, oil, many tyres, spares and tools.

Hold (starboard)
0
0
0
75
0
0
0
0
0
0
77
This is the place where the crew, composed by three men and two women, sleeps. You see their berths, separated by curtains, small cabinets and two portholes.

Crew's quarters
75
0
0
0
0
0
0
0
0
0
80
Food and a reserve of drinking water is stored here, kept in boxes placed in compartments.

Pantry
0
81
0
0
0
0
0
0
0
0
81
This is the kingdom of Youssef, the cook of the Dynamene. You see a large stove, a basin and a workbench.

Galley
80
82
0
0
72
0
0
0
0
0
82
This place is very hot and contains the main ship's engine and ancillary equipment: steam generators, a large water reservoir, fire extinguishers, etc.

Engine room
81
83
0
0
0
0
0
0
0
0
83
Ferry steam engines burn coal. A large quantity is stored there, in bunkers separated from the rest of the Dynamene by an air-tight bulkhead. In case of fire, water can be pumped in from the bottom to extinguish it.

Coal bunker
82
0
0
0
0
0
0
0
0
0
84
The pirate's ship is much smaller than the Dynamene, but is faster. Nobody is around, all the pirates have boarded the Dynamene, not having left much here.

The bridge of the pirate ship
0
0
0
0
71
0
0
0
0
0
90
This place is a filthy harbour used by fishermen and small boats in Alexandria's suburb. There are old fish boats and piles of rubbish everywhere.^M^MYou can go: west, east

Fishing harbour
0
0
91
92
0
0
0
0
0
0
91
You stand on the deck of the pirate's ship. There is the cannon you spiked, but now you notice a hatch to inspect the engine and store some luggage.^M^MYou can go: west

Pirate's ship
0
0
0
90
0
0
0
0
0
0
92
You are in a crowded and smelly tavern. Fishermen go there when they finish their work at sea and to drink some pints from the bar.^M^MYou can go: east, west

Tavern
0
0
90
93
0
0
0
0
0
0
93
Back room^M^MYou can go: east

Back room
0
0
92
0
0
0
0
0
0
0
102
You have spent weeks working with Briccorosso to prepare for the Silk Road rally. It's now July 23, 1907. Four days ago, you boarded a steam ferry called Dynamene to get to Alexandria, in Egypt. It contains the car and everything needed for the expedition. So far, it has been a nice cruise; you spent a lot of time with Briccorosso studying maps and routes between Egypt and India. It's the last evening you will spend onboard: tomorrow morning you are supposed to berth at Alexandria.^M^MTo move on the Dynamene, use fore (f), aft (a), port (p) and starboard (s).

Introduction
0
0
0
0
0
0
0
0
0
0
103
You and captain Maalouf interrogate the two criminals. They are poor guys looking for easy money and they did not know much. You only learn that they use a fishing harbour not far from where the Dynamene is berthed. Once on shore, you leave the crew to deliver the two criminals to the local police. In the meantime, you and captain Maalouf leave to search for the hostages.

Part 4
0
0
0
0
0
0
0
0
0
0
130
Enter password

Introduction
0
0
0
0
0
0
0
0
0
0
MESSAGGI
61
Certainly not here!
66
You don't want to disturb him. He's occupied.
122
You won't need violence to complete this part of the game.
231
You may try, but it will probably not work.
400
No. You aren't tired now.
401
No need to do that now.
402
Use 'bye' to stop playing.
403
Oh, no, we are not going to stay here all day!
407
You should have learned how to play this game, by now.
408
Please, type a more specific action...
504
Please feel free to continue, if you wish to... I've become impervious to swearing and profanity a long time ago.
505
No, you don't feel the need.
600
Khalil Maalouf is the captain and is an athletic man, about 40 years old. He is respected by the crew.
601
He must be 24 or 25 years old, is slightly taller than you, with brown hair and a wispy beard. He has a gentle stare in his brown eyes. You are quite attracted to him.
602
You haven't yet had the opportunity to talk to Albert alone and nobody is around. You look at him seriously and you say to him, coldly:^M- Mr. Pinel, I received your message while I was in Turin. I would like to inform you that I am perfectly able to decide for myself when to take part to an adventure or not.^M^MAlbert looks stunned and mortified.^M^M- Euh, I meant... I wanted to... I'm sorry, I did not want to...^M^MYou then smile and you kiss him intensely. Albert looks even more stunned, yet full of joy.^M^M- Oh, Emilia, I love you, I have loved you since the first moment I saw you in the rose garden, I love you...^M^M- I know...^M^MYou both stay embraced and kissing for a while, but then you stop, as you don't want to be seen. You both continue to stare into each other's eyes in the amber light of one of the most beautiful sunsets the Mediterranean sea can offer to its lucky travellers.
603
Princess Lucilla Briccorosso is 35 years old and is an energetic woman, born into one of the oldest Italian families. She has short blonde hair, is relatively thin and not very tall, but her body is athletic and her green eyes shine with a light of determination.
604
Youssef is the cook of the Dynamene. He has the physique du r√¥le you would expect: large shoulders and belly.
605
- Youssef, what are you preparing?^M- Oh, hello Madam! Sarah caught some nice sea bream this afternoon and I am preparing them with a side dish of escarole endive stewed Neapolitan style.^M^MThe smell is delicious!
606
A stare from the cook tells you it's better not explore the pantry while he can see you.
607
Vittorio is the youngest member of the crew and is nicknamed Toyo. He works swiftly and does not speak very much.
608
Sarah is tall, about 30 years old, wearing a working suit. She keeps her red hair tied up and is greasing the steam engine.
609
No, she's working.
610
It's a large triple-action engine. Steam comes from the steam generator next to the coal bunker.
611
- We will berth tomorrow morning in Alexandria, captain?^M- Yes, the sea will be calm tonight and I expect we will be there at around 8 in the morning.
612
You hear Youssef calling: dinner is served! You meet the crew, the captain, princess Briccorosso and Albert in the dining room.^M^MDynamene offers some luxuries such as silver cutlery for distinguished guests such as Briccorosso; the sea bream are excellent and so is the endive. It's the last evening you will spend aboard and the crew members have been your friends and family for the last four days. After eating, the captain returns to his duties and relieves Neda, so she can take her turn eating.^M^MYou spend the rest of the evening listening to Youssef, a great storyteller who has had many adventures sailing the seven seas. You tried to act casually, but you intercept Albert's stare once or twice.^M^MAt the end of the evening, everybody not on duty goes to rest. You go to your cabin, too, but you can't sleep, thinking of Albert.^M
613
When you open the door, you hear a loud thump. You run to the main deck: a small boat is close and has shot a grappling hook that caught the railing. It sports a 5cm cannon pointing towards the Dynamene. Captain Maalouf comes, cursing.^M^M- I've heard bands of marauders rob small ferries. A shot from that cannon would probably go through us from side to side.^M^MYou hear an order shouted from the boat:^M^M- Stop engines or we will sink you.^M^MYou have to follow the order and four armed individuals come aboard.^M^M- Captain remains with us. Everybody else to their cabins or we shoot!^MWhile an unceremonious pirate escorts you to your cabin, you hear the leader of the pirates asking captain Maalouf:^M^M- Where is the princess?^M^MAh, they know about Briccorosso!
614
You see the pirates' ship not too far below. There is a rope ladder on the bulwark, close to the porthole.
615
No, the door is blocked and those criminals have left someone lookout.
616
You open the porthole, noticing you can almost reach the ladder.
617
You keep your personal belongings there. It's closed, now.
618
You open your closet, to see if you can find something useful inside.
619
You see your luggage.
620
You search your luggage for something useful. You see your umbrella.
621
You are heading towards Egypt, but you are Piemontese, after all. Not really menacing, but maybe it can cause some harm?
622
Don't you think you have talked enough with him?
623
Yay! With the umbrella you pull the rope ladder towards you. You can descend to the pirates' ship, as nobody remained there.
624
Examined in detail, the cannon does not seem so impressive. It is probably 50 years old and has to be ignited with a linstock through its touch hole.
625
You would like to talk a lot with him, but not now.
626
No, somebody is there!
627
You examine the hatch to see if there is a way to get inside, you notice a manhole close to it.
628
There is a service ladder, below.
629
It's the main loading hatch. Through it, the holds have been loaded with the crane.
630
It looks promising.
631
The door leading to the crew's quarters is shut.
632
Among the tools, you find a hammer and some large nails.
633
You insert one nail in the touch hole. It is the right size to completely block it. Covered by the noise of the sea, you hit it with the hammer and you break it off inside. This cannon will not be usable any time soon!
634
Good idea, but better unload the cannon, first.
635
Indeed, but how?
636
You push and pull the cannon a little, it can move to absorb the recoil. After a while, you realize that you can lower the muzzle and the ball rolls to the deck and falls into the sea, followed by the wadding and a little bit of gunpowder which remains on the deck.^MThat's great, but not enough! The pirates would certainly reload it in a matter of minutes.
637
You search among the canisters and you find some wax paper supposed to be used to close the jars of paraffin.
638
The door is kept closed by a large padlock.
639
You pour some gunpowder in the padlock. You hope the explosion will blow it apart, allowing the door to open.
640
You find a box of long matches.
650
That's an... explosive idea!
651
You insert the head of one match in the padlock full of gunpowder. With some apprehension, you light a second match and you ignite the first one. Then you run to the starboard hold, hiding yourself behind the car. You hear a loud BANG! When you return, the padlock is open!
652
You see a kitchen knife on the workbench. Better than nothing.
653
The explosion will bring the pirates here. You need other arms for the crew.
654
Hurray! You find a small gun hidden in a desk drawer.
655
^MThe crew seems to be surprised yet relieved to see you. You say:^M^M- I've found this knife and the captain's gun, we can fight the pirates!^M^MNeda smiles and answers:^M- Well, we had hidden some... let's say... hardware... for special occasions. We were trying to open the door when you blew the padlock.^M^MYou notice some large caliber guns, definitely much better than yours. Then you hear steps coming.^M^MTwo criminals enter, but they are easily surprised and disarmed by the crew.^MSarah and Toyo take charge of them. You and the rest of the crew then run to the main deck. Unfortunately, the two remaining pirates fight back and take Princess Briccorosso and Albert as hostages. As they board their boat you hear a curse when they notice what you did to the cannon. Then they leave, kidnapping your friend and your love.^MYou are angry at this !^M^MThe password for the fourth part of the game is SHIBBOLETH FINIANTLABAGNA
656
The explosion may attract the criminals. You must definitely do something to neutralize their cannon first.
657
You see the cylinder, the shackle and the body, torn apart.
658
Solid!
659
What you are you thinking? You should go in the crew's quarter immediately. Someone is coming from the central room!
660
This place, as everywhere below deck, is lit by small flat-wick paraffin lamps equipped with their characteristic glass chimney.
661
No!
662
No need to hurry!
663
No, that's enough for the moment. You will create a situation later.
664
You see a metallic table solidly secured to the ground, with wooden chairs next to it.
665
No, leave it alone.
666
You see the waves of the Mediterranean sea, tinted by the delicate light of sunset.
667
- Emilia, I can't wait to arrive in Egypt. We will have plenty of work to do!
668
You have worked on those maps for weeks, now! You almost know them by heart.
669
Slightly younger than the captain, Neda is a small woman with black hair and black eyes.
670
Roy is the oldest member of the crew, he must be about 60 and has white hair and beard.
671
A solid wooden bed, secured to the floor.
672
You think it is white oak wood.
673
You are not going to get into the captain's cabin without his permission!
674
The air smells of iodine and salt, with a faint fishy undertone. You breathe it in voluptuously.
675
He must be 24 or 25 years old, is slightly taller than you, with brown hair and a wispy beard. You think you love him...
676
It's a large segmented cover made by several wooden hatch-end beams and covered by a tarp. You can not remove them.
677
You saw this crane lift the ITA 150 and load it delicately in the holds of the Dynamene four days ago.
678
Nothing useful!
679
You may sit there and read a book, but not now.
680
Books about navigation and marine literature. You notice Herman Melville's "Moby Dick"!
681
You give three firm hammer blows to the wedge and the hatch breaks in half. You scream when you open it to inspect the luggage compartment: Albert and Prince Briccorosso are there, bound and gagged. You immediately free them and kiss intensely Albert in front of Princess Briccorosso who laughs:^M^M- Well, it certainly took a while for you both to decide yourselves!
682
You enter the room followed by captain Maalouf, and three armed police officers. You see a little fat individual talking to the two pirates. They are immediately arrested.^M^MTwo hours later, the police discover the fat man is indeed Altiero di Risso. Interrogated, he confesses that he had invested a large quantity of money, through the Banca Commerciale Italiana, in the construction of the Berlin-Baghdad railway. He convinced himself that the Silk Road rally would be very damaging to the railway if it was successful. He feined to participate to the rally as cover so he could sabotage the contestants.^M^MKnowing the determination of Princess Briccorosso, he understood that she would have been his most serious opponent. He paid someone to tamper with the car before delivery in Varenna. Then he paid the pirates to damage the car or throw it into the sea, but they decided to extort some money using the hostages when they learnt that Briccorosso was a princess.^M^MWell, your adventure can continue now. After unloading the ferry you say goodbye to the brave captain Maloouf and to the crew.^M^MNow nothing will stop you participating in the Silk Road rally in three weeks time. But that will be another story...^M^MFinal password: DUIPURUNBAGNANTLOLI
683
Briccorosso says:^M^M- I heard the pirates, they thought I could not understand them, but I learnt their dialect three years ago when I went across the Sahara desert in an expedition. They repeated they must be paid by an Italian not far from here, in a tavern or something. They repeated a name: di Risso.^M^Mdi Risso, di Risso... the director of La voce del mattino, that Milanese newspaper who sent that annoying journalist!
684
- I love you, Albert^M- I love you, too, Emilia!
685
Three agents of the local police, called by your crew, arrive. You decide to search for the criminals together, starting from a nearby tavern.
686
You ask the innkeeper discretely for information about the pirate's boat, but he says he knows nothing.
687
The innkeeper says you can't go there. You may ask the police to force him to let you in, but you aren't sure yet the pirates are in the tavern.
688
You and a police officer start to interrogate the innkeeper. People in the tavern begin to discreetly go away. He repeats that he knows nothing, but starts sweating heavily.
689
The police officers follow you.
690
They are armed and ready for action.
691
Sweat and fish.
692
He seems about 50 years old, with greasy black hair and large shoulders.
693
They expect action, not words.
694
Not impressive, but better than nothing.
695
The furniture is decorated with nice carved wood flowers.
696
What a lovely car!
697
That will be sent forward in advance, to prepare the rally.
698
Large cans of paraffin, sealed with corks and wax paper.
699
The crew sleeps there. Let's hope for them that nobody snores!
700
They separate the berths to give a little privacy.
701
They contain the personal belongings of the crew.
702
It is a small, yet well-equipped, kitchen.
703
Fire is dangerous on a ship!
704
Water becomes steam in the steam generators and is then recovered with a condenser. That moves the engine.
705
Yum! You get hungry!
706
Coal and oil.
707
Safety first!
708
You personally prefer paraffin as a fuel, but at least it's inexpensive.
709
They avoid keeping large quantities of coal together. For safety, you know!
710
However, if you want to stop the pirates, you have to find a way to make it unusable.
711
It's spiked, now. It will not do any harm for a while.
712
There is no reason to go there.
713
The hatch is locked!
714
Good idea! With what?
715
You try to force the wedge between the hatch and its frame, so to break the locker, but it is too blunt and doesn't get inside.
716
You put the sharp wedge between the hatch and its frame, it gets inside perfectly. Now you need something to force it further down.
717
It's a scrap piece of oak wood, whose shape is approximately a wedge.
718
Now its shape is considerably sharper.
719
Yuck! The only thing you notice there is a piece of wood, approximately shaped as a wedge.
720
You notice a set of silver cutlery in the basin, behind the bar.
721
You pick up a fork and you recognize it: there is the Dynamene name engraved on it. Those were stolen by the pirates! You have to visit the back room!
996
You see nothing interesting.
997
You don't smell anything in particular, apart from a barely perceptible scent of lavender, coming from your clothes.
998
You have a strange sensation, almost as if you were becoming lighter and lighter. After a while, everything becomes normal, again.
999
No time for that, now.
1000
Okay.
1001
Done.
1002
Done.
1003
a
1004
a
1005
You can't take that!
1006
You can't do that!
1007
You don't have that with you!
1008
You can't go that way!
1009
I beg your pardon?
1010
You can't.
1011
You already have that with you!
1012
What do you want to do?
1013

1018
I wear 
1019

1020
You can go: 
1021
fore
1022
aft
1023
starboard
1024
port
1025
up
1026
down
1027

1028

1029

1030

1031
You notice: 
1032
You have with you: 
1033
nothing.
1035

OGGETTI
24

Princess Briccorosso
0
0
70
TRUE
TRUE
25

Albert Pinel
0
0
63
TRUE
TRUE
29

ITA model 150
0
0
76
TRUE
FALSE
186

Captain Maalouf
0
0
60
TRUE
TRUE
187

Youssef, the cook
0
0
81
TRUE
TRUE
188

Sarah, the machinist
0
0
82
TRUE
TRUE
189

Toyo, the stoker
0
0
83
TRUE
TRUE
190

Neda, the second-in-command
0
0
60
TRUE
TRUE
191

Roy, the boatswain
0
0
60
TRUE
TRUE
192

A large steam engine
0
0
82
TRUE
TRUE
193

Your luggage
0
0
0
TRUE
TRUE
194

An umbrella
0
0
0
FALSE
TRUE
195

A 50mm cannon
0
0
84
TRUE
TRUE
196

A manhole
0
0
0
TRUE
TRUE
197

A hammer
0
0
0
FALSE
TRUE
198

Some large nails
0
0
0
FALSE
TRUE
199

Some gunpowder
0
0
0
FALSE
TRUE
240

Silver cutlery
0
0
0
FALSE
TRUE
241

A wooden wedge
0
0
0
FALSE
TRUE
242

A sharp wooden wedge
0
0
0
FALSE
TRUE
243

Wax paper
0
0
0
FALSE
TRUE
244

Wax paper
0
0
0
FALSE
TRUE
245

Wax paper with gunpowder
0
0
0
FALSE
TRUE
246

A box of long matches
0
0
0
FALSE
TRUE
247

A large padlock
0
0
0
TRUE
TRUE
248

A small gun
0
0
0
FALSE
TRUE
249

A kitchen knife
0
0
0
FALSE
TRUE
250

A tall pirate
0
0
0
TRUE
TRUE
251

A scarred pirate
0
0
0
TRUE
TRUE
252

Pieces of a blown padlock
0
0
0
FALSE
TRUE
254

Three armed police officers
0
0
0
TRUE
TRUE
255

The innkeeper
0
0
92
TRUE
TRUE
FINEDATI